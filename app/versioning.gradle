ext.getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--long'
            /* Git вернёт вам имя ближайшей метки с числом коммитов,
              сделанных поверх этой метки и частичное значения SHA-1
              описываемого коммита;
              Все тэги --tags;
              Количество коммитов независимо от того тэг ли
              текущий коммит --long
            */
            standardOutput = stdout
        }
        def info = stdout.toString().trim().split('-') /*v1.0.0-21-g975b*/
        return info[0] + '.' + info[1]
    }
    catch (ignored) {
        return '0.0.0'
    }
}

ext.getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--long'
            standardOutput = stdout
        }
        def info = stdout.toString().trim().split('-')
        return info[1].toInteger()
    }
    catch (ignored) {
        return 0
    }
}